import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "idea"
    id "java-library"
    id "maven-publish"
    id "signing"
    id "org.springframework.boot" version "3.3.2"
    id "org.jetbrains.kotlin.jvm" version "2.0.10"
    id "org.jetbrains.kotlin.kapt" version "2.0.20"
    id "io.spring.dependency-management" version "1.1.6"
}

group "io.viascom.devutils"
version "0.0.2"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.bootJar { enabled = false }
tasks.jar { enabled = true }

jar {
    // Remove `plain` postfix from jar file name
    archiveClassifier.set("")
}

java {
    withJavadocJar()
    withSourcesJar()
}

springBoot {
    buildInfo()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    compileOnly "org.springframework.boot:spring-boot-starter-web"
    compileOnly "org.springframework.boot:spring-boot-starter-security"
    compileOnly "org.springframework.boot:spring-boot-starter-data-jpa"

    kapt "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.boot:spring-boot-starter-web"
    testImplementation "org.springframework.boot:spring-boot-starter-security"

    developmentOnly "org.springframework.boot:spring-boot-devtools"
}

test {
    useJUnitPlatform()
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += '-Xjsr305=strict'
        jvmTarget = '21'
    }
}

//Wait for files from processResources and bootBuildInfo
compileKotlin.inputs.files(processResources, bootBuildInfo)

classes {
    doLast {
        // Copy generated spring-configuration-metadata.json into jar resources
        file("build/tmp/kapt3/classes/main/META-INF/spring-configuration-metadata.json").renameTo(file("build/resources/main/META-INF/spring-configuration-metadata.json"))
    }
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                groupId = 'io.viascom.devutils'
                name = 'spring-boot-starter-maintenance'
                description = 'Maintenance mode library for spring boot web and security projects.'
                url = 'https://github.com/viascom/spring-boot-starter-maintenance'
                packaging = 'jar'

                licenses {
                    license {
                        name = 'The Unlicense'
                        url = 'https://unlicense.org/'
                    }
                }

                scm {
                    url = 'https://github.com/viascom/spring-boot-starter-maintenance'
                    connection = 'scm:git://github.com:viascom/spring-boot-starter-maintenance.git'
                    developerConnection = 'scm:git://github.com:viascom/spring-boot-starter-maintenance.git'
                }

                developers {
                    developer {
                        id = 'nik-sta'
                        name = 'Nikola Stankovic'
                        email = 'nikola.stankovic@viascom.email'
                        organizationUrl = 'https://viascom.io/'
                    }
                    developer {
                        id = 'itsmefox'
                        name = 'Patrick BÃ¶sch'
                        email = 'patrick.boesch@viascom.email'
                        organizationUrl = 'https://viascom.io/'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign).configureEach {
    onlyIf { isReleaseVersion }
}